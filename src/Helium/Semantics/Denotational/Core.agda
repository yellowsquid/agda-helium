------------------------------------------------------------------------
-- Agda Helium
--
-- Base definitions for the denotational semantics.
------------------------------------------------------------------------

{-# OPTIONS --safe --without-K #-}

open import Helium.Data.Pseudocode.Types using (RawPseudocode)

module Helium.Semantics.Denotational.Core
  {bâ‚ bâ‚‚ iâ‚ iâ‚‚ iâ‚ƒ râ‚ râ‚‚ râ‚ƒ}
  (rawPseudocode : RawPseudocode bâ‚ bâ‚‚ iâ‚ iâ‚‚ iâ‚ƒ râ‚ râ‚‚ râ‚ƒ)
  where

private
  open module C = RawPseudocode rawPseudocode

open import Data.Bool as Bool using (Bool; true; false)
open import Data.Fin as Fin using (Fin; zero; suc; #_)
import Data.Fin.Properties as Finâ‚š
open import Data.Nat as â„• using (â„•; zero; suc)
import Data.Nat.Properties as â„•â‚š
open import Data.Product as P using (_Ã—_; _,_)
open import Data.Sum as S using (_âŠ_) renaming (injâ‚ to next; injâ‚‚ to ret)
open import Data.Unit using (âŠ¤)
open import Data.Vec as Vec using (Vec; []; _âˆ·_)
open import Data.Vec.Relation.Unary.All using (All; []; _âˆ·_)
import Data.Vec.Functional as VecF
open import Function using (case_of_; _âˆ˜â€²_; id)
open import Helium.Data.Pseudocode.Core
open import Helium.Semantics.Core rawPseudocode
import Induction as I
import Induction.WellFounded as Wf
open import Relation.Binary.PropositionalEquality as â‰¡ using (_â‰¡_; module â‰¡-Reasoning)
open import Relation.Nullary using (does)
open import Relation.Nullary.Decidable.Core using (True; False; toWitness; fromWitness)

-- The case for bitvectors looks odd so that the right-most bit is bit 0.
ğ’¦ : âˆ€ {t} â†’ Literal t â†’ âŸ¦ t âŸ§â‚œ
ğ’¦ (x â€²b)  = x
ğ’¦ (x â€²i)  = x â„¤â€².Ã—â€² 1â„¤
ğ’¦ (x â€²f)  = x
ğ’¦ (x â€²r)  = x â„â€².Ã—â€² 1â„
ğ’¦ (xs â€²x) = Vec.foldl Bits (Î» bs b â†’ (Bool.if b then 1ğ”¹ else 0ğ”¹) VecF.âˆ· bs) VecF.[] xs
ğ’¦ (x â€²a)  = Vec.replicate (ğ’¦ x)

updateAt : âˆ€ {n} ts i â†’ âŸ¦ Vec.lookup ts i âŸ§â‚œ â†’ âŸ¦ tuple n ts âŸ§â‚œ â†’ âŸ¦ tuple n ts âŸ§â‚œ
updateAt (_ âˆ· [])     zero    v _        = v
updateAt (_ âˆ· _ âˆ· _)  zero    v (_ , xs) = v , xs
updateAt (_ âˆ· t âˆ· ts) (suc i) v (x , xs) = x , updateAt (t âˆ· ts) i v xs

tupCons : âˆ€ {n t} ts â†’ âŸ¦ t âŸ§â‚œ â†’ âŸ¦ tuple n ts âŸ§â‚œ â†’ âŸ¦ tuple _ (t âˆ· ts) âŸ§â‚œ
tupCons []       x xs = x
tupCons (t âˆ· ts) x xs = x , xs

tupHead : âˆ€ {n t} ts â†’ âŸ¦ tuple (suc n) (t âˆ· ts) âŸ§â‚œ â†’ âŸ¦ t âŸ§â‚œ
tupHead {t = t} ts xs = fetch (t âˆ· ts) xs zero

tupTail : âˆ€ {n t} ts â†’ âŸ¦ tuple _ (t âˆ· ts) âŸ§â‚œ â†’ âŸ¦ tuple n ts âŸ§â‚œ
tupTail []      x        = _
tupTail (_ âˆ· _) (x , xs) = xs

equal : âˆ€ {t} â†’ HasEquality t â†’ âŸ¦ t âŸ§â‚œ â†’ âŸ¦ t âŸ§â‚œ â†’ Bool
equal bool x y = does (x Bool.â‰Ÿ y)
equal int  x y = does (x â‰Ÿá¶» y)
equal fin  x y = does (x Fin.â‰Ÿ y)
equal real x y = does (x â‰ŸÊ³ y)
equal bits x y = does (x â‰Ÿáµ‡ y)

comp : âˆ€ {t} â†’ IsNumeric t â†’ âŸ¦ t âŸ§â‚œ â†’ âŸ¦ t âŸ§â‚œ â†’ Bool
comp int  x y = does (x <á¶»? y)
comp real x y = does (x <Ê³? y)

-- 0 of y is 0 of result
join : âˆ€ t {m n} â†’ âŸ¦ asType t m âŸ§â‚œ â†’ âŸ¦ asType t n âŸ§â‚œ â†’ âŸ¦ asType t (n â„•.+ m) âŸ§â‚œ
join bits      {m} x y = y VecF.++ x
join (array _) {m} x y = y Vec.++ x

-- take from 0
take : âˆ€ t i {j} â†’ âŸ¦ asType t (i â„•.+ j) âŸ§â‚œ â†’ âŸ¦ asType t i âŸ§â‚œ
take bits      i x = VecF.take i x
take (array _) i x = Vec.take i x

-- drop from 0
drop : âˆ€ t i {j} â†’ âŸ¦ asType t (i â„•.+ j) âŸ§â‚œ â†’ âŸ¦ asType t j âŸ§â‚œ
drop bits      i x = VecF.drop i x
drop (array _) i x = Vec.drop i x

casted : âˆ€ t {i j} â†’ .(eq : i â‰¡ j) â†’ âŸ¦ asType t i âŸ§â‚œ â†’ âŸ¦ asType t j âŸ§â‚œ
casted bits                  eq x       = x âˆ˜â€² Fin.cast (â‰¡.sym eq)
casted (array _) {j = zero}  eq []      = []
casted (array t) {j = suc _} eq (x âˆ· y) = x âˆ· casted (array t) (â„•â‚š.suc-injective eq) y

module _ where
  mâ‰¤nâ‡’m+kâ‰¡n : âˆ€ {m n} â†’ m â„•.â‰¤ n â†’ P.âˆƒ Î» k â†’ m â„•.+ k â‰¡ n
  mâ‰¤nâ‡’m+kâ‰¡n â„•.zâ‰¤n       = _ , â‰¡.refl
  mâ‰¤nâ‡’m+kâ‰¡n (â„•.sâ‰¤s mâ‰¤n) = P.dmap id (â‰¡.cong suc) (mâ‰¤nâ‡’m+kâ‰¡n mâ‰¤n)

  slicedSize : âˆ€ i j (off : Fin (suc i)) â†’ P.âˆƒ Î» k â†’ i â„•.+ j â‰¡ Fin.toâ„• off â„•.+ (j â„•.+ k)
  slicedSize i j off = k , (begin
    i â„•.+ j                   â‰¡Ë˜âŸ¨ â‰¡.cong (â„•._+ j) (P.projâ‚‚ off+kâ‰¤i) âŸ©
    (Fin.toâ„• off â„•.+ k) â„•.+ j â‰¡âŸ¨  â„•â‚š.+-assoc (Fin.toâ„• off) k j âŸ©
    Fin.toâ„• off â„•.+ (k â„•.+ j) â‰¡âŸ¨  â‰¡.cong (Fin.toâ„• off â„•.+_) (â„•â‚š.+-comm k j) âŸ©
    Fin.toâ„• off â„•.+ (j â„•.+ k) âˆ)
    where
    open â‰¡-Reasoning
    off+kâ‰¤i = mâ‰¤nâ‡’m+kâ‰¡n (â„•â‚š.â‰¤-pred (Finâ‚š.toâ„•<n off))
    k = P.projâ‚ off+kâ‰¤i

  sliced : âˆ€ t {i j} â†’ âŸ¦ asType t (i â„•.+ j) âŸ§â‚œ â†’ âŸ¦ fin (suc i) âŸ§â‚œ â†’ âŸ¦ asType t j âŸ§â‚œ
  sliced t {i} {j} x off = take t j (drop t (Fin.toâ„• off) (casted t (P.projâ‚‚ (slicedSize i j off)) x))

updateSliced : âˆ€ {a} {A : Set a} t {i j} â†’ âŸ¦ asType t (i â„•.+ j) âŸ§â‚œ â†’ âŸ¦ fin (suc i) âŸ§â‚œ â†’ âŸ¦ asType t j âŸ§â‚œ â†’ (âŸ¦ asType t (i â„•.+ j) âŸ§â‚œ â†’ A) â†’ A
updateSliced t {i} {j} orig off v f = f (casted t (â‰¡.sym eq) (join t (join t untaken v) dropped))
  where
  eq = P.projâ‚‚ (slicedSize i j off)
  dropped = take t (Fin.toâ„• off) (casted t eq orig)
  untaken = drop t j (drop t (Fin.toâ„• off) (casted t eq orig))

neg : âˆ€ {t} â†’ IsNumeric t â†’ âŸ¦ t âŸ§â‚œ â†’ âŸ¦ t âŸ§â‚œ
neg int  x = â„¤.- x
neg real x = â„.- x

add : âˆ€ {tâ‚ tâ‚‚} (isNumâ‚ : True (isNumeric? tâ‚)) (isNumâ‚‚ : True (isNumeric? tâ‚‚)) â†’ âŸ¦ tâ‚ âŸ§â‚œ â†’ âŸ¦ tâ‚‚ âŸ§â‚œ â†’ âŸ¦ combineNumeric tâ‚ tâ‚‚ {isNumâ‚} {isNumâ‚‚} âŸ§â‚œ
add {tâ‚ = int}  {tâ‚‚ = int}  _ _ x y = x â„¤.+ y
add {tâ‚ = int}  {tâ‚‚ = real} _ _ x y = x /1 â„.+ y
add {tâ‚ = real} {tâ‚‚ = int}  _ _ x y = x â„.+ y /1
add {tâ‚ = real} {tâ‚‚ = real} _ _ x y = x â„.+ y

mul : âˆ€ {tâ‚ tâ‚‚} (isNumâ‚ : True (isNumeric? tâ‚)) (isNumâ‚‚ : True (isNumeric? tâ‚‚)) â†’ âŸ¦ tâ‚ âŸ§â‚œ â†’ âŸ¦ tâ‚‚ âŸ§â‚œ â†’ âŸ¦ combineNumeric tâ‚ tâ‚‚ {isNumâ‚} {isNumâ‚‚} âŸ§â‚œ
mul {tâ‚ = int}  {tâ‚‚ = int}  _ _ x y = x â„¤.* y
mul {tâ‚ = int}  {tâ‚‚ = real} _ _ x y = x /1 â„.* y
mul {tâ‚ = real} {tâ‚‚ = int}  _ _ x y = x â„.* y /1
mul {tâ‚ = real} {tâ‚‚ = real} _ _ x y = x â„.* y

pow : âˆ€ {t} â†’ IsNumeric t â†’ âŸ¦ t âŸ§â‚œ â†’ â„• â†’ âŸ¦ t âŸ§â‚œ
pow int  x n = x â„¤â€².^â€² n
pow real x n = x â„â€².^â€² n

shiftr : 2 â„â€².Ã—â€² 1â„ â„.â‰‰ 0â„ â†’ âŸ¦ int âŸ§â‚œ â†’ â„• â†’ âŸ¦ int âŸ§â‚œ
shiftr 2â‰‰0 x n = âŒŠ x /1 â„.* 2â‰‰0 â„.â»Â¹ â„â€².^â€² n âŒ‹

module Expression
  {o} {Î£ : Vec Type o}
  (2â‰‰0 : 2 â„â€².Ã—â€² 1â„ â„.â‰‰ 0â„)
  where

  open Code Î£

  âŸ¦_âŸ§áµ‰ : âˆ€ {n} {Î“ : Vec Type n} {t} â†’ Expression Î“ t â†’ âŸ¦ Î£ âŸ§â‚œâ€² â†’ âŸ¦ Î“ âŸ§â‚œâ€² â†’ âŸ¦ t âŸ§â‚œ
  âŸ¦_âŸ§Ë¢ : âˆ€ {n} {Î“ : Vec Type n} â†’ Statement Î“ â†’ âŸ¦ Î£ âŸ§â‚œâ€² â†’ âŸ¦ Î“ âŸ§â‚œâ€² â†’ âŸ¦ Î£ âŸ§â‚œâ€² Ã— âŸ¦ Î“ âŸ§â‚œâ€²
  âŸ¦_âŸ§á¶  : âˆ€ {n} {Î“ : Vec Type n} {ret} â†’ Function Î“ ret â†’ âŸ¦ Î£ âŸ§â‚œâ€² â†’ âŸ¦ Î“ âŸ§â‚œâ€² â†’ âŸ¦ ret âŸ§â‚œ
  âŸ¦_âŸ§áµ– : âˆ€ {n} {Î“ : Vec Type n} â†’ Procedure Î“ â†’ âŸ¦ Î£ âŸ§â‚œâ€² â†’ âŸ¦ Î“ âŸ§â‚œâ€² â†’ âŸ¦ Î£ âŸ§â‚œâ€²
  âŸ¦_âŸ§áµ‰â€² : âˆ€ {n} {Î“ : Vec Type n} {m ts} â†’ All (Expression Î“) ts â†’ âŸ¦ Î£ âŸ§â‚œâ€² â†’ âŸ¦ Î“ âŸ§â‚œâ€² â†’ âŸ¦ tuple m ts âŸ§â‚œ
  update : âˆ€ {n Î“ t e} â†’ CanAssign {n} {Î“} {t} e â†’ âŸ¦ t âŸ§â‚œ â†’ âŸ¦ Î£ âŸ§â‚œâ€² â†’ âŸ¦ Î“ âŸ§â‚œâ€² â†’ âŸ¦ Î£ âŸ§â‚œâ€² Ã— âŸ¦ Î“ âŸ§â‚œâ€²

  âŸ¦ lit x âŸ§áµ‰ Ïƒ Î³ = ğ’¦ x
  âŸ¦ state i âŸ§áµ‰ Ïƒ Î³ = fetch Î£ Ïƒ (# i)
  âŸ¦_âŸ§áµ‰ {Î“ = Î“} (var i) Ïƒ Î³ = fetch Î“ Î³ (# i)
  âŸ¦ abort e âŸ§áµ‰ Ïƒ Î³ = case âŸ¦ e âŸ§áµ‰ Ïƒ Î³ of Î» ()
  âŸ¦ _â‰Ÿ_ {hasEquality = hasEq} e eâ‚ âŸ§áµ‰ Ïƒ Î³ = equal (toWitness hasEq) (âŸ¦ e âŸ§áµ‰ Ïƒ Î³) (âŸ¦ eâ‚ âŸ§áµ‰ Ïƒ Î³)
  âŸ¦ _<?_ {isNumeric = isNum} e eâ‚ âŸ§áµ‰ Ïƒ Î³ = comp (toWitness isNum) (âŸ¦ e âŸ§áµ‰ Ïƒ Î³) (âŸ¦ eâ‚ âŸ§áµ‰ Ïƒ Î³)
  âŸ¦ inv e âŸ§áµ‰ Ïƒ Î³ = Bool.not (âŸ¦ e âŸ§áµ‰ Ïƒ Î³)
  âŸ¦ e && eâ‚ âŸ§áµ‰ Ïƒ Î³ = Bool.if âŸ¦ e âŸ§áµ‰ Ïƒ Î³ then âŸ¦ eâ‚ âŸ§áµ‰ Ïƒ Î³ else false
  âŸ¦ e || eâ‚ âŸ§áµ‰ Ïƒ Î³ = Bool.if âŸ¦ e âŸ§áµ‰ Ïƒ Î³ then true else âŸ¦ eâ‚ âŸ§áµ‰ Ïƒ Î³
  âŸ¦ not e âŸ§áµ‰ Ïƒ Î³ = Bits.Â¬_ (âŸ¦ e âŸ§áµ‰ Ïƒ Î³)
  âŸ¦ e and eâ‚ âŸ§áµ‰ Ïƒ Î³ = âŸ¦ e âŸ§áµ‰ Ïƒ Î³ Bits.âˆ§ âŸ¦ eâ‚ âŸ§áµ‰ Ïƒ Î³
  âŸ¦ e or eâ‚ âŸ§áµ‰ Ïƒ Î³ = âŸ¦ e âŸ§áµ‰ Ïƒ Î³ Bits.âˆ¨ âŸ¦ eâ‚ âŸ§áµ‰ Ïƒ Î³
  âŸ¦ [ e ] âŸ§áµ‰ Ïƒ Î³ = âŸ¦ e âŸ§áµ‰ Ïƒ Î³ Vec.âˆ· []
  âŸ¦ unbox e âŸ§áµ‰ Ïƒ Î³ = Vec.head (âŸ¦ e âŸ§áµ‰ Ïƒ Î³)
  âŸ¦ _âˆ¶_ {t = t} e eâ‚ âŸ§áµ‰ Ïƒ Î³ = join t (âŸ¦ e âŸ§áµ‰ Ïƒ Î³) (âŸ¦ eâ‚ âŸ§áµ‰ Ïƒ Î³)
  âŸ¦ slice {t = t} e eâ‚ âŸ§áµ‰ Ïƒ Î³ = sliced t (âŸ¦ e âŸ§áµ‰ Ïƒ Î³) (âŸ¦ eâ‚ âŸ§áµ‰ Ïƒ Î³)
  âŸ¦ cast {t = t} eq e âŸ§áµ‰ Ïƒ Î³ = casted t eq (âŸ¦ e âŸ§áµ‰ Ïƒ Î³)
  âŸ¦ -_ {isNumeric = isNum} e âŸ§áµ‰ Ïƒ Î³ = neg (toWitness isNum) (âŸ¦ e âŸ§áµ‰ Ïƒ Î³)
  âŸ¦ _+_ {isNumericâ‚ = isNumâ‚} {isNumericâ‚‚ = isNumâ‚‚} e eâ‚ âŸ§áµ‰ Ïƒ Î³ = add isNumâ‚ isNumâ‚‚ (âŸ¦ e âŸ§áµ‰ Ïƒ Î³) (âŸ¦ eâ‚ âŸ§áµ‰ Ïƒ Î³)
  âŸ¦ _*_ {isNumericâ‚ = isNumâ‚} {isNumericâ‚‚ = isNumâ‚‚} e eâ‚ âŸ§áµ‰ Ïƒ Î³ = mul isNumâ‚ isNumâ‚‚ (âŸ¦ e âŸ§áµ‰ Ïƒ Î³) (âŸ¦ eâ‚ âŸ§áµ‰ Ïƒ Î³)
  -- âŸ¦ e / eâ‚ âŸ§áµ‰ Ïƒ Î³ = {!!}
  âŸ¦ _^_ {isNumeric = isNum} e n âŸ§áµ‰ Ïƒ Î³ = pow (toWitness isNum) (âŸ¦ e âŸ§áµ‰ Ïƒ Î³) n
  âŸ¦ _>>_ e n âŸ§áµ‰ Ïƒ Î³ = shiftr 2â‰‰0 (âŸ¦ e âŸ§áµ‰ Ïƒ Î³) n
  âŸ¦ rnd e âŸ§áµ‰ Ïƒ Î³ = âŒŠ âŸ¦ e âŸ§áµ‰ Ïƒ Î³ âŒ‹
  âŸ¦ fin x e âŸ§áµ‰ Ïƒ Î³ = apply x (âŸ¦ e âŸ§áµ‰ Ïƒ Î³)
    where
    apply : âˆ€ {k ms n} â†’ (All Fin ms â†’ Fin n) â†’ âŸ¦ Vec.map {n = k} fin ms âŸ§â‚œâ€² â†’ âŸ¦ fin n âŸ§â‚œ
    apply {zero}  {[]}     f xs = f []
    apply {suc k} {_ âˆ· ms} f xs =
      apply (Î» x â†’ f (tupHead (Vec.map fin ms) xs âˆ· x)) (tupTail (Vec.map fin ms) xs)
  âŸ¦ asInt e âŸ§áµ‰ Ïƒ Î³ = Fin.toâ„• (âŸ¦ e âŸ§áµ‰ Ïƒ Î³) â„¤â€².Ã—â€² 1â„¤
  âŸ¦ tup [] âŸ§áµ‰ Ïƒ Î³ = _
  âŸ¦ tup (e âˆ· []) âŸ§áµ‰ Ïƒ Î³ = âŸ¦ e âŸ§áµ‰ Ïƒ Î³
  âŸ¦ tup (e âˆ· eâ€² âˆ· es) âŸ§áµ‰ Ïƒ Î³ = âŸ¦ e âŸ§áµ‰ Ïƒ Î³ , âŸ¦ tup (eâ€² âˆ· es) âŸ§áµ‰ Ïƒ Î³
  âŸ¦ call f e âŸ§áµ‰ Ïƒ Î³ = âŸ¦ f âŸ§á¶  Ïƒ (âŸ¦ e âŸ§áµ‰â€² Ïƒ Î³)
  âŸ¦ if e then eâ‚ else eâ‚‚ âŸ§áµ‰ Ïƒ Î³ = Bool.if âŸ¦ e âŸ§áµ‰ Ïƒ Î³ then âŸ¦ eâ‚ âŸ§áµ‰ Ïƒ Î³ else âŸ¦ eâ‚‚ âŸ§áµ‰ Ïƒ Î³

  âŸ¦ [] âŸ§áµ‰â€²          Ïƒ Î³ = _
  âŸ¦ e âˆ· [] âŸ§áµ‰â€²      Ïƒ Î³ = âŸ¦ e âŸ§áµ‰ Ïƒ Î³
  âŸ¦ e âˆ· eâ€² âˆ· es âŸ§áµ‰â€² Ïƒ Î³ = âŸ¦ e âŸ§áµ‰ Ïƒ Î³ , âŸ¦ eâ€² âˆ· es âŸ§áµ‰â€² Ïƒ Î³

  âŸ¦ s âˆ™ sâ‚ âŸ§Ë¢ Ïƒ Î³ = P.uncurry âŸ¦ s âŸ§Ë¢ (âŸ¦ s âŸ§Ë¢ Ïƒ Î³)
  âŸ¦ skip âŸ§Ë¢ Ïƒ Î³ = Ïƒ , Î³
  âŸ¦ _â‰”_ ref {canAssign = canAssign} e âŸ§Ë¢ Ïƒ Î³ = update (toWitness canAssign) (âŸ¦ e âŸ§áµ‰ Ïƒ Î³) Ïƒ Î³
  âŸ¦_âŸ§Ë¢ {Î“ = Î“} (declare e s) Ïƒ Î³ = P.mapâ‚‚ (tupTail Î“) (âŸ¦ s âŸ§Ë¢ Ïƒ (tupCons Î“ (âŸ¦ e âŸ§áµ‰ Ïƒ Î³) Î³))
  âŸ¦ invoke p e âŸ§Ë¢ Ïƒ Î³ = âŸ¦ p âŸ§áµ– Ïƒ (âŸ¦ e âŸ§áµ‰â€² Ïƒ Î³) , Î³
  âŸ¦ if e then sâ‚ else sâ‚‚ âŸ§Ë¢ Ïƒ Î³ = Bool.if âŸ¦ e âŸ§áµ‰ Ïƒ Î³ then âŸ¦ sâ‚ âŸ§Ë¢ Ïƒ Î³ else âŸ¦ sâ‚‚ âŸ§Ë¢ Ïƒ Î³
  âŸ¦_âŸ§Ë¢ {Î“ = Î“} (for m s) Ïƒ Î³ = helper m âŸ¦ s âŸ§Ë¢ Ïƒ Î³
    where
    helper : âˆ€ m â†’ (âŸ¦ Î£ âŸ§â‚œâ€² â†’ âŸ¦ fin m âˆ· Î“ âŸ§â‚œâ€² â†’ âŸ¦ Î£ âŸ§â‚œâ€² Ã— âŸ¦ fin m âˆ· Î“ âŸ§â‚œâ€²) â†’ âŸ¦ Î£ âŸ§â‚œâ€² â†’ âŸ¦ Î“ âŸ§â‚œâ€² â†’ âŸ¦ Î£ âŸ§â‚œâ€² Ã— âŸ¦ Î“ âŸ§â‚œâ€²
    helper zero    s Ïƒ Î³ = Ïƒ , Î³
    helper (suc m) s Ïƒ Î³ = P.uncurry (helper m sâ€²) (P.mapâ‚‚ (tupTail Î“) (s Ïƒ (tupCons Î“ zero Î³)))
      where
      sâ€² : âŸ¦ Î£ âŸ§â‚œâ€² â†’ âŸ¦ fin m âˆ· Î“ âŸ§â‚œâ€² â†’ âŸ¦ Î£ âŸ§â‚œâ€² Ã— âŸ¦ fin m âˆ· Î“ âŸ§â‚œâ€²
      sâ€² Ïƒ Î³ =
        P.mapâ‚‚ (tupCons Î“ (tupHead Î“ Î³) âˆ˜â€² (tupTail Î“))
               (s Ïƒ (tupCons Î“ (suc (tupHead Î“ Î³)) (tupTail Î“ Î³)))

  âŸ¦ s âˆ™return e âŸ§á¶  Ïƒ Î³ = P.uncurry âŸ¦ e âŸ§áµ‰ (âŸ¦ s âŸ§Ë¢ Ïƒ Î³)
  âŸ¦_âŸ§á¶  {Î“ = Î“} (declare e f) Ïƒ Î³ = âŸ¦ f âŸ§á¶  Ïƒ (tupCons Î“ (âŸ¦ e âŸ§áµ‰ Ïƒ Î³) Î³)

  âŸ¦ s âˆ™end âŸ§áµ– Ïƒ Î³ = P.projâ‚ (âŸ¦ s âŸ§Ë¢ Ïƒ Î³)

  update (state i {i<o}) v Ïƒ Î³ = updateAt Î£ (#_ i {m<n = i<o}) v Ïƒ , Î³
  update {Î“ = Î“} (var i {i<n}) v Ïƒ Î³ = Ïƒ , updateAt Î“ (#_ i {m<n = i<n}) v Î³
  update abort v Ïƒ Î³ = Ïƒ , Î³
  update (_âˆ¶_ {m = m} {t = t} e eâ‚) v Ïƒ Î³ = do
    let Ïƒâ€² , Î³â€² = update e (sliced t v (Fin.fromâ„• _)) Ïƒ Î³
    update eâ‚ (sliced t (casted t (â„•â‚š.+-comm _ m) v) zero) Ïƒâ€² Î³â€²
  update [ e ] v Ïƒ Î³ = update e (Vec.head v) Ïƒ Î³
  update (unbox e) v Ïƒ Î³ = update e (v âˆ· []) Ïƒ Î³
  update (slice {t = t} {eâ‚ = eâ‚} a eâ‚‚) v Ïƒ Î³ = updateSliced t (âŸ¦ eâ‚ âŸ§áµ‰ Ïƒ Î³) (âŸ¦ eâ‚‚ âŸ§áµ‰ Ïƒ Î³) v (Î» v â†’ update a v Ïƒ Î³)
  update (cast {t = t} eq e) v Ïƒ Î³ = update e (casted t (â‰¡.sym eq) v) Ïƒ Î³
  update (tup {es = []} x) v Ïƒ Î³ = Ïƒ , Î³
  update (tup {es = _ âˆ· []} (x âˆ· [])) v Ïƒ Î³ = update x v Ïƒ Î³
  update (tup {es = _ âˆ· _ âˆ· _} (x âˆ· xs)) (v , vs) Ïƒ Î³ = do
    let Ïƒâ€² , Î³â€² = update x v Ïƒ Î³
    update (tup xs) vs Ïƒâ€² Î³â€²
